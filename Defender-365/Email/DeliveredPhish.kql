// Identify delivered incoming emails categorised by 365 Defender as Phishing or Malware with a high confidence 
EmailEvents
| where ThreatTypes in ('Phish', 'Malware')
| where EmailAction !in ('Replace attachment', 'Send to quarantine')
| where EmailDirection == "Inbound"
| where ConfidenceLevel contains "High" 
// --- Whitelist ---//
//| where SenderFromAddress !contains "example.com"  // Whitelisted domain example
//| where SenderFromAddress !contains "anotherexample.com"  // Another whistlisted domain example
// --- End Whitelist --- //
| summarize FirstEmail = min(Timestamp), LatestEmail = max(Timestamp), Count = count(), Recipients = make_list(RecipientEmailAddress) by
//--- Uncomment next line to expand each email instance and time of delivery to each recipient ---//
    //Timestamp, RecipientEmailAddress, EnvelopeSenderAddress = SenderMailFromAddress,
    Subject,
    Threat = ThreatTypes,
    Reason = extractjson("$.Phish[0]", DetectionMethods),
    HeaderFromAddress = SenderFromAddress,
    DeliveryAction,
    Attachments = AttachmentCount
| project-reorder Threat, Subject, HeaderFromAddress, Reason, Recipients
| sort by LatestEmail
