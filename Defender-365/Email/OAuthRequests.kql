// Identify emails that link to a Microsoft OAuth login
// This activity is commonly used in phishing campaigns to grant permissions to a 3rd party without stealing credentials
// OAuth authentication will present users with a Windows or Microsoft Online website to authenticate, which makes the activity appear genuine
EmailUrlInfo  
| where Url contains "https://login.windows.net/common/oauth2" or Url contains "https://login.microsoftonline.com/consumers/oauth2" 
| where Url contains "redirect_uri"
| join EmailEvents  on $left.NetworkMessageId == $right.NetworkMessageId
| where EmailDirection == "Inbound"
| project TimeGenerated, Url, EmailDirection, RecipientEmailAddress, SenderDisplayName, SenderFromAddress, SenderIPv4, Subject, UrlLocation, DeliveryAction, DeliveryLocation
